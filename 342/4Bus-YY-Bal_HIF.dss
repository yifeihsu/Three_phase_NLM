clear

! IEEE 4-bus test case   Y-Y Stepdown Balanced
! Based on script developed by Alan Dunn and Steve Sparling

new circuit.4BusYYbal basekV=12.47 phases=3 
! **** HAVE TO STIFFEN THE SOURCE UP A LITTLE; THE TEST CASE ASSUMES AN INFINITE BUS
~ mvasc3=200000 200000

set earthmodel=carson

! **** DEFINE WIRE DATA
new wiredata.conductor Runits=mi Rac=0.306 GMRunits=ft GMRac=0.0244  Radunits=in Diam=0.721 
new wiredata.neutral   Runits=mi Rac=0.592 GMRunits=ft GMRac=0.00814 Radunits=in Diam=0.563 

! **** DEFINE LINE GEOMETRY; REDUCE OUT THE NEUTRAL
new linegeometry.4wire nconds=4 nphases=3 reduce=yes 
~ cond=1 wire=conductor units=ft x=-4   h=28 
~ cond=2 wire=conductor units=ft x=-1.5 h=28 
~ cond=3 wire=conductor units=ft x=3    h=28 
~ cond=4 wire=neutral   units=ft x=0    h=24 

! **** 12.47 KV LINE
new line.1 geometry=4wire length=2000 units=ft bus1=sourcebus bus2=n2

! **** 3-PHASE STEP-DOWN TRANSFORMER 12.47/4.16 KV Y-Y
new transformer.t1 xhl=6
~ wdg=1 bus=n2 conn=wye kV=12.47 kVA=6000 %r=0.5 
~ wdg=2 bus=n3 conn=wye kV=4.16  kVA=6000 %r=0.5 

! **** 4.16 KV LINE
new line.2 bus1=n3 bus2=n4 geometry=4wire length=2500 units=ft

! **** WYE-CONNECTED 4.16 KV LOAD
New Load.1 Phases=1 Bus1=n4.1 kW=1275 PF=.85 kV=2.4 vminpu=0.74
New Load.2 Phases=1 Bus1=n4.2 kW=1800 PF=.90 kV=2.4 vminpu=0.74
New Load.3 Phases=1 Bus1=n4.3 kW=2375 PF=.95 kV=2.4 vminpu=0.74

! **** HAVE TO ALLOW P, Q TO REMAIN CONSTANT TO ABOUT .79 PU -- THIS IS ASSUMED IN TEST CASE
! **** DEFAULT IN DSS IS .95, BELOW WHICH IT REVERTS TO LINEAR MODEL

New Linecode.Kersting nphases=3  Units=mi
~ Rmatrix=[0.457552  |0.155951  0.466628  |0.153485  0.158007  0.461473  ]
~ Xmatrix=[1.07805  |0.501679  1.04818  |0.384938  0.423653  1.06507  ]
~ Cmatrix=[15.0675  |-4.86254  15.8754  |-1.85328  -3.09107  14.3258  ]

! Edit line.1 Linecode=Kersting
! Edit line.2 Linecode=Kersting


! ------------- Mid‑span HIF auto‑generated block -----------
Edit line.2 enabled=no
New line.2_a bus1=n3 bus2=FaultBus length=1250.0000 units=ft geometry=4wire 
New line.2_b bus1=FaultBus bus2=n4 length=1250.0000 units=ft geometry=4wire 
New Load.HIF_Load Bus1=FaultBus.1 Phases=1 Conn=Wye Model=2 kW=38.4569 kvar=0 kV=2.402
! -----------------------------------------------------------

set voltagebases=[12.47, 4.16] 
calcvoltagebases     ! **** let DSS compute voltage bases
solve
